
FUNCTION(TARGET_LINK_FLAGS_PORTABLE LIBRARY)
  IF(${CMAKE_VERSION} VERSION_GREATER_EQUAL "3.13")
    #great, this works the "right" way
    TARGET_LINK_OPTIONS(${LIBRARY} ${ARGN})
  ELSE()
    #bummer, this works the "hacky" way
    TARGET_LINK_LIBRARIES(${LIBRARY} ${ARGN})
  ENDIF()
ENDFUNCTION(TARGET_LINK_FLAGS_PORTABLE)

FUNCTION(KOKKOSKERNELS_LINK_TPLS LIBRARY)
  IF (KOKKOSKERNELS_ENABLE_TPL_BLAS)
    TARGET_LINK_LIBRARIES(${LIBRARY} PUBLIC ${BLAS_LIBRARIES})
    TARGET_LINK_FLAGS_PORTABLE(${LIBRARY} PUBLIC ${BLAS_LINK_FLAGS})
  ENDIF()

  IF (KOKKOSKERNELS_ENABLE_TPL_CUBLAS)
    TARGET_LINK_LIBRARIES(${LIBRARY} PUBLIC ${CUDA_CUBLAS_LIBRARIES})
  ENDIF()

  IF (KOKKOSKERNELS_ENABLE_TPL_CUSPARSE)
    TARGET_LINK_LIBRARIES(${LIBRARY} PUBLIC ${CUDA_cusparse_LIBRARY})
  ENDIF()
ENDFUNCTION(KOKKOSKERNELS_LINK_TPLS)

KOKKOSKERNELS_ADD_TPL_OPTION(BLAS OFF "Whether to enable BLAS")
KOKKOSKERNELS_ADD_TPL_OPTION(MKL  OFF "Whether to enable MKL")
KOKKOSKERNELS_ADD_TPL_OPTION(CUBLAS   ${KOKKOS_ENABLE_CUDA} "Whether to enable CUBLAS")
KOKKOSKERNELS_ADD_TPL_OPTION(CUSPARSE ${KOKKOS_ENABLE_CUDA} "Whether to enable CUSPARSE")
KOKKOSKERNELS_ADD_TPL_OPTION(MAGMA    ${KOKKOS_ENABLE_CUDA} "Whether to enable MAGMA")

IF (KOKKOSKERNELS_ENABLE_TPL_BLAS OR KOKKOSKERNELS_ENABLE_TPL_MKL)
  ENABLE_LANGUAGE(C)
  ENABLE_LANGUAGE(Fortran)
  INCLUDE(FortranCInterface)
  SET(F77_BLAS_MANGLE "(name,NAME) ${FortranCInterface_GLOBAL_PREFIX}name ## ${FortranCInterface_GLOBAL_SUFFIX}")
ENDIF()

IF (KOKKOSKERNELS_ENABLE_TPL_BLAS)
  IF (NOT KOKKOSKERNELS_HAS_TRILINOS)
    FIND_PACKAGE(BLAS REQUIRED)
  ENDIF()
  LIST(APPEND TPL_LIST "BLAS")
ENDIF()

IF (KOKKOSKERNELS_ENABLE_TPL_MKL)
  IF (NOT KOKKOSKERNELS_HAS_TRILINOS)
  #  FIND_PACKAGE(MKL REQUIRED)
  ENDIF()
  LIST(APPEND TPL_LIST "MKL")
ENDIF()

IF (KOKKOS_ENABLE_CUDA AND NOT KOKKOSKERNELS_HAS_TRILINOS)
  FIND_PACKAGE(CUDA REQUIRED)
ENDIF()

IF (KOKKOSKERNELS_ENABLE_TPL_CUBLAS)
  LIST(APPEND TPL_LIST "CUBLAS")
ENDIF()

IF (KOKKOSKERNELS_ENABLE_TPL_CUSPARSE)
  LIST(APPEND TPL_LIST "CUSPARSE")
ENDIF()

IF (KOKKOSKERNELS_ENABLE_TPL_MAGMA)
  IF (KOKKOSKERNELS_HAS_TRILINOS)
    IF (F77_BLAS_MANGLE STREQUAL "(name,NAME) name ## _")
      SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DADD_ -fopenmp -lgfortran")
    ELSEIF (F77_BLAS_MANGLE STREQUAL "(name,NAME) NAME")
      SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DUPCASE -fopenmp -lgfortran")
    ELSEIF (F77_BLAS_MANGLE STREQUAL "(name,NAME) name")
      SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DNOCHANGE -fopenmp -lgfortran")
    ELSE ()
      MESSAGE(FATAL_ERROR "F77_BLAS_MANGLE ${F77_BLAS_MANGLE} detected while MAGMA only accepts Fortran mangling that is one of single underscore (-DADD_), uppercase (-DUPCASE), and no change (-DNOCHANGE)")
    ENDIF()
  ELSE()
    MESSAGE(FATAL_ERROR "KokkosKernels does not currently support MAGMA in standalone mode")
  ENDIF()
  LIST(APPEND TPL_LIST "MAGMA")
ENDIF()

